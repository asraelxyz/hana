record JSON

    true = record
        to_json = |self| "true"
    end
    false = record
        to_json = |self| "false"
    end

    func parse(str)
        chars = str.chars()
        slen = chars.length()
        r = Record()
        r.i = 0
        ws = func()
            while r.i < slen and (chars[r.i] == ' ' or chars[r.i] == '\t')
                r.i += 1
            end
        end
        next = func(ch)
            if r.i < slen and chars[r.i] == ch begin
                r.i += 1
                return
            end
            raise JSON::ParserError("Expected " + ch + ", got " + chars[r.i])
        end
        r.parse_number = func(r)
            num = ""
            while r.i < slen and chars[r.i] >= "0" and chars[r.i] <= "9" begin
                num += chars[r.i]
                r.i += 1
            end
            if r.i < slen and chars[r.i] == "." begin
                num += "."
                r.i += 1
                while r.i < slen and chars[r.i] >= "0" and chars[r.i] <= "9" begin
                    num += chars[r.i]
                    r.i += 1
                end
                return Float(num)
            end
            return Int(num)
        end
        r.parse_str = func(r)
            next('"')
            ss = ""
            while r.i < slen begin
                if chars[r.i] == '"' begin
                    r.i += 1
                    return ss
                end
                else begin
                    ss += chars[r.i]
                    r.i += 1
                end
            end
        end
        r.parse_word = func(r)
            c = chars[r.i]
            if c == "t" begin
                next("t")
                next("r")
                next("u")
                next("e")
                return JSON.true
            end
            else if c == "f" begin
                next("f")
                next("a")
                next("l")
                next("s")
                next("e")
                return JSON.false
            end
            else if c == "n" begin
                next("n")
                next("u")
                next("l")
                next("l")
                return nil
            end
            raise JSON::ParserError("expected word")
        end
        r.parse_value = func(r)
            ws()
            if chars[r.i] == "{"      then return r.parse_object()
            else if chars[r.i] == "[" then return r.parse_array()
            else if chars[r.i] == '"' then return r.parse_str()
            else if chars[r.i] >= "0" and chars[r.i] <= "9" then return r.parse_number()
            else return r.parse_word()
        end
        r.parse_array = func(r)
            key = ""
            L = []
            next("[")
            ws()
            if chars[r.i] == "]" then return L
            while r.i < slen begin
                L.push(r.parse_value())
                ws()
                if r.i < slen and chars[r.i] == "]" begin
                    r.i += 1
                    return L
                end
                next(",")
                ws()
            end
        end
        r.parse_object = func(r)
            key = ""
            obj = Record()
            r.i += 1
            ws()
            if chars[r.i] == "}" then return obj
            while r.i < slen begin
                key = r.parse_str()
                ws()
                next(":")
                ws()
                value = r.parse_value()
                obj[key] = value
                ws()
                if r.i < slen and chars[r.i] == "}" begin
                    r.i += 1
                    return obj
                end
                next(",")
                ws()
            end
        end
        return r.parse_object()
    end

    func serialise(obj)
        if obj of Record begin
            if Record::has_key(obj, "to_json") then return obj.to_json()
            return Record::to_json(obj)
        end
        return obj.to_json()
    end

    // errors
    record ParserError
        func constructor(self, what?)
            self.what? = what?
            return self
        end
    end

end

// serialising
Record.to_json = func(self)
    s = "{"
    a = []
    keys = Record::keys(self)
    for key in keys begin
        a.push(JSON::serialise(key) + ":" + JSON::serialise(self[key]))
    end
    s += a.join(", ")
    s += "}"
    return s
end

Array.to_json = func(self)
    return "[" + self.map(|v| {
        return JSON::serialise(v)
    }).join(", ") + "]"
end

String.to_json = func(self)
    return '"' + self.chars().map(|v| {
        if v == '"' then return '\\"'
        else return v
    }).join('') + '"'
end

Int.to_json = func(self)
    return String(self)
end
Float.to_json = Int.to_json